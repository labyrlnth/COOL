/*
 *  cool.cup
 *              Parser definition for the COOL language.
 *
 */

import java_cup.runtime.*;

/* Stuff enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }
:} 

/************************************************************************/
/*                DONT CHANGE ANYTHING IN THIS SECTION                  */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}

/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */

terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WONT WORK       */
/**************************************************************************/

   /* Complete the nonterminal list below, giving a type for the semantic
      value of each non terminal. (See the CUP documentation for details. */

nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class;
nonterminal Features feature_list;
nonterminal Feature feature;
nonterminal Formals formal_list;
nonterminal Formals non_empty_formal_list;
nonterminal formalc formal;
nonterminal Expression expr;
nonterminal comma_expr_list;
nonterminal non_empty_comma_expr_list;
nonterminal semi_expr_list;
nonterminal at_type;
nonterminal optinal_assign;
nonterminal let_assign_list;
nonterminal let_assign;
nonterminal case_list;
nonterminal case_stmt;

/* Precedence declarations go here. */
precedence left PLUS, MINUS;
precedence left MULT, DIV;

program	
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
        ;

class_list
	/* single class */
	::= class:c
	    {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
	/* several classes */
	| class_list:cl class:c
	    {: RESULT = cl.appendElement(c); :}
	;

/* If no parent is specified, the class inherits from the Object class */
class
	::= CLASS TYPEID:n LBRACE feature_list:fl RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, 
		                   AbstractTable.idtable.addString("Object"), 
				   fl, curr_filename()); :}
	| CLASS TYPEID:n INHERITS TYPEID:p LBRACE feature_list:fl RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, p, fl, curr_filename()); :}
	;

/* Feature list may be empty, but no empty features in list. */
feature_list
	::= /* empty */
	    {: RESULT = new Features(curr_lineno()); :}
	| feature_list:fl feature:f
		{: RESULT = fl.appendElement(f); :}
	;

feature 
	::= OBJECTID:oid LPAREN formal_list:fl RPAREN COLON TYPEID:tid LBRACE expr:e RBRACE SEMI
		{: RESULT = new method(curr_lineno(), oid, fl, tid, e); :}
	| OBJECTID:oid COLON TYPEID:tid SEMI
		{: RESULT = new attr(curr_lineno(), oid, tid, new no_expr(curr_lineno())); :}
	| OBJECTID:oid COLON TYPEID:tid ASSIGN expr:e SEMI
		{: RESULT = new attr(curr_lineno(), oid, tid, e); :}
	;

formal_list
	::= /* empty */
		{: RESULT = new Formals(curr_lineno()); :}
	| non_empty_formal_list:fl
		{: RESULT = fl; :}
	;


non_empty_formal_list
	::= formal:f
		{: RESULT = new Formals(curr_lineno()).appendElement(f);:}
	| non_empty_formal_list:fl COMMA formal:f
		{: RESULT = fl.appendElement(f); :}
	;

formal
	::= OBJECTID:oid COLON TYPEID:tid
		{: RESULT = new formalc(curr_lineno(), oid, tid); :}
	;

expr
	::= OBJECTID:oid ASSIGN expr:e
		{: RESULT = new assign(curr_lineno(), oid, e); :}
	| expr at_type DOT OBJECTID LPAREN comma_expr_list RPAREN
	| OBJECTID LPAREN comma_expr_list RPAREN
	| IF expr THEN expr ELSE expr FI
	| WHILE expr LOOP expr POOL
	| LBRACE semi_expr_list RBRACE
	| LET let_assign_list IN expr
	| CASE expr OF case_list ESAC
	| NEW TYPEID:tid
		{: RESULT = new new_(curr_lineno(), tid); :}
	| ISVOID expr:e
		{: RESULT = new isvoid(curr_lineno(), e); :}
	| expr:e1 PLUS expr:e2
		{: RESULT = new plus(curr_lineno(), e1, e2); :}
	| expr:e1 MINUS expr:e2
		{: RESULT = new sub(curr_lineno(), e1, e2); :}
	| expr:e1 MULT expr:e2
		{: RESULT = new mul(curr_lineno(), e1, e2); :}
	| expr:e1 DIV expr:e2
		{: RESULT = new divide(curr_lineno(), e1, e2); :}
	| NEG expr
	| expr LT expr
	| expr LE expr
	| expr EQ expr
	| NOT expr:e
	| LPAREN expr:e RPAREN
		{: RESULT = e; :}
	| OBJECTID:oid
		{: RESULT = new object(curr_lineno(), oid); :}
	| INT_CONST:i
		{: RESULT = new int_const(curr_lineno(), i); :}
	| STR_CONST:s
		{: RESULT = new string_const(curr_lineno(), s); :}
	| BOOL_CONST:b
		{: RESULT = new bool_const(curr_lineno(), b); :}
	;

at_type
	::= /* empty */
	| AT TYPEID
	;

comma_expr_list
	::= /* empty */
	| non_empty_comma_expr_list
	;


non_empty_comma_expr_list
	::= expr
	| expr COMMA non_empty_comma_expr_list
	;

semi_expr_list
	::= expr SEMI
	| expr SEMI semi_expr_list
	;

optinal_assign
	::= /* empty */
	| ASSIGN expr
	;

let_assign_list
	::= let_assign
	| let_assign COMMA let_assign_list
	;

let_assign
	::= OBJECTID COLON TYPEID optinal_assign
	;

case_list
	::= case_stmt
	| case_stmt case_list
	;

case_stmt
	::= OBJECTID COLON TYPEID DARROW expr SEMI
	;
